import java.time.LocalDateTime
import java.time.Month
import java.time.temporal.ChronoUnit

apply plugin: 'com.android.application'

android {
    namespace 'su.xash.tf15client'
    ndkVersion = "27.2.12479018"
    compileSdk 35

    defaultConfig {
        applicationId 'su.xash.tf15client'
        versionName '0.99'
        versionCode getBuildNum()
        minSdkVersion 21
        targetSdk 35
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    externalNativeBuild {
        cmake {
            version '3.22.1'
            path file('../../CMakeLists.txt')
        }
    }

    lint {
        abortOnError false
    }

    androidResources {
        noCompress += ""
    }

    sourceSets {
        main {
            assets {
                srcDirs += "${project.projectDir}/../../3rdparty/tf15client-extras"
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled = false
            shrinkResources = false
            debuggable = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }

        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }
}

static def getBuildNum() {
    LocalDateTime now = LocalDateTime.now()
    LocalDateTime releaseDate = LocalDateTime.of(2018, Month.DECEMBER, 7, 0, 0, 0)
    int qBuildNum = releaseDate.until(now, ChronoUnit.DAYS)
    int minuteOfDay = now.getHour() * 60 + now.getMinute()
    return qBuildNum * 10000 + minuteOfDay
}